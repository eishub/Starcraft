use TerranSCV as knowledge.
use "../GenericMods/GenericPercepts.mod2g" as module.


module TerranSCVEvent {
	%%%%%%%%%%%%%%% --------------------------------------- Messaging --------------------------------------- %%%%%%%%%%%%%%%
	
	% If don't know location of the Command Center yet, request location to Raynor
	if not(bel(commandCenterPosition(_, _))) then (raynor).send?(commandCenterPosition).
	
	% If received commandCenter location, insert it to knowledge base
	if (_).sent:(commandCenterPosition(X, Y)) then insert(commandCenterPosition(X, Y)).
	
	%%%%%%%%%%%%%%% --------------------------------------- Percepts --------------------------------------- %%%%%%%%%%%%%%%
		
	% The generic percepts most units use
	if true then GenericPercepts.	
	
	% Handling of the condition percept
	if bel(condition(List)), percept(condition(OList)) then delete(condition(List)) + insert(condition(OList)).
	
	% The handling of the supply percept
	if percept(resources(_, _, C, T)), bel(supply(OC, OT), (not(OC == C) ; not(OT == T))) then delete(supply(OC, OT)) + insert(supply(C, T)).
	
	% The handling of mineralfields	
	forall percept(mineralField(Id,Amount,_,X,Y)), not(bel(mineralField(Id,Amount,X,Y))) do insert(mineralField(Id,Amount,X,Y)).
	forall bel(mineralField(Id,Amount,X,Y)), not(percept(mineralField(Id,Amount,_,X,Y))) do delete(mineralField(Id,Amount,X,Y)).
	
	% The handling of the constructionSites
	forall percept(constructionSite(X,Y)), not(bel(constructionSite(X,Y))) do insert(constructionSite(X,Y)).
	forall bel(constructionSite(X,Y)),not(percept(constructionSite(X,Y))) do delete(constructionSite(X,Y)).
	
	% The handling of the workerActivity
	forall percept(workerActivity(ID,ACT)), bel(not(workerActivity(ID,ACT))) do insert(workerActivity(ID,ACT)).
	forall bel(workerActivity(ID,ACT)),not(percept(workerActivity(ID,ACT))) do delete(workerActivity(ID,ACT)).
	
	% The handling of vespeneGeyser percept
	if percept(vespeneGeyser(_, _, _, X , Y)), bel(not(vespeneGeyser(X ,Y))) then insert(vespeneGeyser(X ,Y)).
	if bel(vespeneGeyser(X ,Y)),not(percept(vespeneGeyser(_, _, _, X , Y))) then delete(vespeneGeyser(X ,Y)).
	
	% The handling of the friendly units percept
	forall bel(unitCondition(Type, Condition1)) , not(percept(unit(true, Type,_,_,_,Condition2))), bel(Condition1 \= Condition2)
	do delete(unitCondition(Type, Condition1)).
	forall percept(unit(true, Type,_,_,_,Condition)), bel(not(unitCondition(Type,Condition))) 
	do insert(unitCondition(Type, Condition)).
		
	%%%%%%%%%%%%%%% --------------------------------------- GOALS --------------------------------------- %%%%%%%%%%%%%%%
	
	% If there are 8 scv units, start building barracks
	if bel(builder ; (backupBuilder, unitAmount("Terran Barracks", 1)), unitAmount("Terran SCV", Amount), Amount > 7) 
	then adopt(unitAmount("Terran Barracks", 2)).
	
	% If there are 9 scv units, start adding some supply
	if bel(builder, unitAmount("Terran SCV", 9), unitAmount("Terran Barracks",2)) then adopt(unitAmount("Terran Supply Depot", 1)).
	
	% If there are 2 barracks start making an academy
	if bel(backupBuilder, unitAmount("Terran Barracks", 2), not(unitAmount("Terran Academy", 1))) then adopt(unitAmount("Terran Academy", 1)).
	
	if bel(backupBuilder, unitAmount("Terran Academy", 1)) then adopt(unitAmount("Terran Barracks", 3)).
	
	% Build Supply Depots for supply
	if not(goal(unitAmount("Terran Supply Depot", _))), bel(builder, unitAmount("Terran Supply Depot", N), M is N +1, unitAmount("Terran Barracks", 2), 
	supply(CS, TS), TS - CS < 8, condition(List), not(member(constructing, List))) then adopt(unitAmount("Terran Supply Depot", M)).
	
	% If there is a vespeneGeyser near base, build a refinery
	if bel(vespeneGeyser(X ,Y), not(builder), not(busy), commandCenterPosition(BX, BY) ,distance(BX, BY, X, Y, D), D < 10,
	unitAmount("Terran Barracks", 2)) then adopt(unitAmount("Terran Refinery", 1)).
	
	if bel(vespeneMiner, self(MyId,_), not(workerActivity(MyId, gatheringGas))) then adopt(workerActivity(MyId, gatheringGas)).
	
	% If the worker is not busy yet, start gathering minerals
	if bel(not(busy), self(Id, _)) then adopt(workerActivity(Id, gatheringMinerals)).
	
	%%%%%%%%%%%%%%% --------------------------------------- Messaging --------------------------------------- %%%%%%%%%%%%%%%
	
	% If there is a refinery and not enough scv units are gathering gas, adopt goal to do so
	if bel(shouldMineVespene, gas(G), G < 50 , not(unitAmount("Terran Medic", _))) then (raynor).send?(mineVespene).
	
	% If told to mine vespene, insert in knowledge base vespeneMiner 
	if (_).sent!(mineVespene) then insert(vespeneMiner).
	
	% If told to be backupBuilder, insert it to knowledge base
	if (_).sent(backupBuilder) then insert(backupBuilder).
	
	% If told to be builder insert it to knowledge base
	if (_).sent(builder) then insert(builder).
}